buildscript {
    repositories {
		mavenCentral()
		maven { url "${OBP2ReadOnlyMavenRepository}" }
    }
    dependencies {
        classpath group: 'fr.ensta', name: 'ecore2java', version: '1.5'
    }
}
apply plugin: 'fr.ensta.ecore2java'
apply plugin: 'java'

version = '1.0.0'

repositories {
	mavenCentral()
	maven { url "${OBP2ReadOnlyMavenRepository}" }
}

configurations {
	antlr4
	ecore
	compile.extendsFrom antlr4
	compile.extendsFrom ecore
}
dependencies {
	antlr4  group: 'org.antlr', name: 'antlr4' , version: '4.8'

	compile group: 'org.eclipse.emf', name: 'org.eclipse.emf.common',               version: '2.11.0-v20150123-0347'
	compile group: 'org.eclipse.emf', name: 'org.eclipse.emf.ecore',                version: '2.11.0-v20150123-0347'
	compile group: 'org.eclipse.emf', name: 'org.eclipse.emf.ecore.xmi',            version: '2.11.0-v20150123-0347'
	// https://mvnrepository.com/artifact/junit/junit
	testCompile group: 'junit', name: 'junit', version: '4.7'

}

String antlr4OutputPath(String path) {
	if (System.properties['os.name'].replaceAll(' ', '').toLowerCase().contains('win')) {
		return file(path + '/grammar')
	}
	return file(path)
}
def generatedPath = file('generated')
generatedPath.mkdir()
task generateAntlr4Parser(type: JavaExec) {
	classpath configurations.antlr4
	main = "org.antlr.v4.Tool"
	args = ["grammar/DVE.g4"
		, "-package"
		, "DVE.grammar"
		, "-o"
		, antlr4OutputPath("generated/DVE")]
}

ecore2java {
    genModelPath = file('model/DVE.genmodel')
    outputDir = generatedPath
}

sourceSets.main.java.srcDir 'generated'
compileJava.dependsOn generateAntlr4Parser, ecore2java

clean { doLast { project.delete "tmp", "generated" } }
